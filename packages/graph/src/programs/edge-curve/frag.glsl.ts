/* istanbul ignore file */

// language=GLSL
const SHADER = /* glsl */ `
precision highp float;

varying vec4 v_color;
varying float v_opacity;
varying float v_thickness;
varying vec2 v_cpA;
varying vec2 v_cpB;
varying vec2 v_cpC;


float det(vec2 a, vec2 b) {
  return a.x * b.y - b.x * a.y;
}

vec2 getDistanceVector(vec2 b0, vec2 b1, vec2 b2) {
  float a = det(b0, b2), b = 2.0 * det(b1, b0), d = 2.0 * det(b2, b1);
  float f = b * d - a * a;
  vec2 d21 = b2 - b1, d10 = b1 - b0, d20 = b2 - b0;
  vec2 gf = 2.0 * (b * d21 + d * d10 + a * d20);
  gf = vec2(gf.y, -gf.x);
  vec2 pp = -f * gf / dot(gf, gf);
  vec2 d0p = b0 - pp;
  float ap = det(d0p, d20), bp = 2.0 * det(d10, d0p);
  float t = clamp((ap + bp) / (2.0 * a + b + d), 0.0, 1.0);
  return mix(mix(b0, b1, t), mix(b1, b2, t), t);
}

float distToQuadraticBezierCurve(vec2 p, vec2 b0, vec2 b1, vec2 b2) {
  return length(getDistanceVector(b0 - p, b1 - p, b2 - p));
}

const float epsilon = 0.7;
const vec4 transparent = vec4(0.0, 0.0, 0.0, 0.0);

void main(void) {
  float dist = distToQuadraticBezierCurve(gl_FragCoord.xy, v_cpA, v_cpB, v_cpC);
  float thickness = v_thickness;

  if (dist < thickness + epsilon) {
    #ifdef PICKING_MODE
    gl_FragColor = v_color;
    #else
    float inCurve = 1.0 - smoothstep(thickness - epsilon, thickness + epsilon, dist);
    gl_FragColor = inCurve * vec4(v_color.rgb * v_color.a, v_color.a);
    gl_FragColor = mix(transparent, gl_FragColor, v_opacity);
    #endif
  } else {
    gl_FragColor = transparent;
  }
}
`;

export default SHADER;
